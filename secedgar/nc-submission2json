#!/usr/bin/env perl
#
#  Synopsis:
#	JSONify all ^<ELEMENT> fields in non-cooked SEC daily file.
#  Usage:
#	nc-submission2json <9999999997-21-000077.nc
#

use JSON::PP q(encode_json);

my @JSON;
my $ele_re = '[A-Z][A-Z0-9_-]+';

my $line_number = 0;
while (<>) {
	$line_number++;
	last if /^<TEXT>/;
	die "line_number > 10240: may be busted file: $line_number"
		if $line_number > 10240
	;
	next if m/^<\/$ele_re>/;
	unless (m/^<($ele_re)>(.*)/) {
		print STDERR "WARN: line #$line_number: not an element: $_";
		next;
	}
	my ($ele, $val) = ($1, $2);

	#  panic for <ELEMENTS> with chars other than xml valid
	die "element contains unknown chars: $_"
				if $ele =~ m/^<[A-Z].*[^A-Z0-9_-].*>/
	;

	push @JSON, {
		"line_number" => $line_number,
		"element" => $ele,
		"value" => $val
	};
}

print encode_json(\@JSON);
